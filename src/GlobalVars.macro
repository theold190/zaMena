%%GetTabSize=
$If(%tabSize<0)
    editor.pos(1,2,1);
    %tmpBuff=Editor.Value;
    Tab print(%tmpBuff); Enter Up
    %tabSize=len(Editor.Value)-len(%tmpBuff);
    CtrlY
$End

%%GetInitialPosition=
  %positionLine=editor.pos(0,1);
  %positionColumn=editor.pos(0,2);

  %startLine=editor.sel(0,0);
  %startColumn=editor.sel(0,1);
  %endLine=editor.sel(0,2);
  %endColumn=editor.sel(0,3);

%%ShiftCodeBlock=
  %iter=%startShiftLine;
  $While(%iter<=%endShiftLine)
    editor.pos(1,1,%iter);
    editor.pos(1,2,1);
    %buff=Editor.Value;
    %buffLen=len(%buff);

    $If(%buffLen!=0)
      %trimmedBuff=trim(%buff,1);
      %indent=%buffLen-len(%trimmedBuff);
      $If(%indent+%shiftSize > 0)
        $Rep(%indent+%shiftSize)
          print(" ");
        $End
        %shifted=1;
      $End
      print(trim(%buff,1)) Enter CtrlY
    $End
    %iter=%iter+1;
  $End

%%RestoreShiftSelection=
  $If(%shifted==0)
    %shiftSize=0;
  $End

  $If(%endColumn==0)
    editor.pos(1,2,%startColumn);
    editor.pos(1,1,%startLine);
    editor.sel(2,0);

    editor.pos(1,2,%startColumn);
    editor.pos(1,1,%endLine+1);
    editor.sel(2,1);

    editor.pos(1,2,%startColumn);
    editor.pos(1,1,%positionLine);
  $Else
    editor.pos(1,2,%startColumn+%shiftSize);
    editor.pos(1,1,%startLine);
    editor.sel(2,0);

    editor.pos(1,2,%endColumn+1+%shiftSize);
    editor.pos(1,1,%endLine);
    editor.sel(2,1);

    editor.pos(1,2,%positionColumn+%shiftSize);
    editor.pos(1,1,%positionLine);
  $End

%%FormatCodeBlock=
  editor.pos(1,1,%startFormatLine);
  %indentStartFlag=index(Editor.Value,"{");
  editor.pos(1,1,%endFormatLine);
  %indentStopFlag=index(Editor.Value,"}");

  %comment="Flags are indicating that we can somehow handle this block";
  $If(%indentStartFlag != -1 && %indentStopFlag != -1)
    editor.pos(1,1,%startFormatLine);
    %indentColumn=len(Editor.Value)-len(trim(Editor.Value,1));
    %iter=%startFormatLine+1;
    $While(%iter<=%endFormatLine)
      editor.pos(1,1,%iter);
      editor.pos(1,2,1);
      %buff=Editor.Value;
      %currIndent=len(%buff)-len(trim(%buff,1));

      %comment="We have different indent for block lines and a closing bracket";
      $If(%iter != %endFormatLine)
        %reqIndent=%indentColumn+%tabSize;
      $Else
        %reqIndent=%indentColumn;
      $End

      %comment="Now we set the requested indent and add trimmed line";
      $If(%currIndent != %reqIndent)
        $Rep(%reqIndent)
          print(" ");
        $End
        print(trim(%buff,1)); Enter CtrlY
      $End
      %iter=%iter+1;
    $End
  $End
